// ---------------------------------------------------------------------------
// Open Surge Engine
// http://opensurge2d.org
//
// File:   objects/water_airsource.obj
// Desc:   water (air bubble)
// Author: Alexandre
// Date:   2012-02-04
// ---------------------------------------------------------------------------

object "water.air_source"
{
    requires 0.2.0
    category "water"

    state "main"
    {
        set_zindex 0.99
        set_alpha 0.5
        set_animation SD_WATERAIRSOURCE 0
        change_state go
    }

    state "go"
    {
        if "ypos() - 16 <= waterlevel()" hide
        on_timeout 0.5 create_bubble
    }

    state "hide"
    {
        hide
        if "ypos() - 16 <= waterlevel()" hide
        show
        change_state go
    }

    state create_bubble
    {
        if "0 == (($k+=1) mod 16)" create_breathable_bubble
        create_child .water.air_bubble 0 -4
        change_state go
    }

    state create_breathable_bubble
    {
        create_child .water.air_bubble[breathable] 0 -4
        change_state go
    }
}

object ".water.air_bubble"
{
    requires 0.2.0
    destroy_if_far_from_play_area

    state "main"
    {
        hide
        let $amp=3+random(2)
        set_zindex 0.99
        set_alpha 0.5
        set_animation SD_WATERAIRBUBBLE "cond(random(4)==0,0,4)"
        change_state go
    }

    state "go"
    {
        if "ypos() - hotspot_y()/2 <= waterlevel()" destroy
        show
        move "($amp*(2*pi())) * sin(2*pi()*($t+=dt()))" "-30" // the chain rule
    }

    state "destroy"
    {
        destroy
    }
}

object ".water.air_bubble[breathable]"
{
    requires 0.2.0
    destroy_if_far_from_play_area

    state "main"
    {
        hide
        let $amp=2+random(2)
        set_zindex 0.99
        set_alpha 0.5
        set_animation SD_WATERAIRBUBBLE 1
        change_state go
    }

    state "go"
    {
        if "ypos() - hotspot_y() <= waterlevel()" destroy
        show
        move "($amp*(2*pi())) * sin(2*pi()*($t+=dt()))" "-30" // the chain rule
        let "$t+=dt()"
        on_player_collision breathe
    }

    state "destroy"
    {
        destroy
    }

    state "breathe"
    {
        on_water_shield go // why should you use this bubble if you have a water shield?
        if "$t<2" go
        player_breathe
        change_state end
    }

    state "end"
    {
        set_animation SD_WATERAIRBUBBLE 2
        on_animation_finished destroy
    }
}
