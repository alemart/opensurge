// ----------------------------------------
// Open Surge Engine
// http://opensurge2d.org
//
// Peixudo
//
// Peixudo Fish script by Alexandre
//
// Changelog:
// 2011-08-20 - first version by Alexandre
// ----------------------------------------

object "Peixudo"
{
    requires 0.2.0
    category "enemy" "level 1"
    annotation "A fish"
 
    state "main"
    {
        // constants
        let "$gravity = gravity()" // A MAGIC NUMBER. this means: an acceleration, on the y-axis, of $gravity pixels per second squared
        let "$jump_strength = 600" // a maximum speed, on the y-axis, of -$jump_strength pixels per second
        let "$cooldown_time = 2+random(3)" // how long until I jump again
        let "$score = 100" // how much I'm worth

        // variables
        let "$yspeed = 0" // this object will move, on the y-axis, at a rate of $yspeed pixels per second

        // go
        hide
        set_animation "Peixudo" "0"
        change_state "stopped"
    }

    state "stopped"
    {
        on_timeout "$cooldown_time" "play jump sound"
    }

    state "play jump sound"
    {
        let "$volume = 1 - abs(player_xpos() - xpos()) / 640"
        let "$pan = (xpos() - player_xpos()) / 160"
        play_sample "samples/levels/1/peixudo.wav" "$volume" "$pan"
        change_state "jump"
    }

    state "jump"
    {
        show
        let "$yspeed = -$jump_strength"
        change_state "in the air"
    }

    state "in the air"
    {
        enemy "$score"

        // moves the object on the y axis
        move "0" "$yspeed" // move the object at $yspeed pixels per second
        let "$yspeed = $yspeed + $gravity * dt()" // gravity affects the yspeed
        look_at_player
        set_angle "cond($yspeed < 0, 0, 180)"

        // hide again?
        if "ypos() >= spawnpoint_y()" "hide again"
    }

    state "hide again"
    {
        hide
        set_absolute_position "spawnpoint_x()" "spawnpoint_y()"
        change_state "stopped"
    }
}
